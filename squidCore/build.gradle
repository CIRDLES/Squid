
// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

// NetBeans will automatically add "run" and "debug" tasks relying on the
// "mainClass" property. You may however define the property prior executing
// tasks by passing a "-PmainClass=<QUALIFIED_CLASS_NAME>" argument.
//
// Note however, that you may define your own "run" and "debug" task if you
// prefer. In this case NetBeans will not add these tasks but you may rely on
// your own implementation.


import java.text.SimpleDateFormat
import java.util.Date
import org.apache.tools.ant.filters.*

if (!hasProperty('mainClass')) {
    ext.mainClass = ''
}

apply plugin: 'antlr'

dependencies {
    compile 'com.github.cirdles:Commons:bc38781605'
    compile "org.apache.commons:commons-math3:3.6.1"   
    compile group: 'com.google.guava', name: 'guava', version: '25.1-jre'
    
    compile "com.github.cirdles:LudwigLibrary:6569c60873"
    compile group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.9'
    
    antlr "org.antlr:antlr4:4.7.1"
    compile group: 'org.antlr', name: 'antlr4-runtime', version: '4.7.1'
    
    // https://mvnrepository.com/artifact/org.apache.poi/poi
    compile group: 'org.apache.poi', name: 'poi', version: '3.16'

    compile group: 'org.jdom', name: 'jdom2', version: '2.0.6'
    
    compile 'com.github.opencollab.jlatexmath:jlatexmath:1.0.7'
    compile group: 'com.openhtmltopdf', name: 'openhtmltopdf-pdfbox', version: '0.0.1-RC7'

    compile('javax.xml.bind:jaxb-api:2.3.0')
    compile('javax.activation:activation:1.1')
    compile('org.glassfish.jaxb:jaxb-runtime:2.3.0')

}

sourceSets {
    main {
        antlr {
            srcDirs = ["src/main/antlr"]
        }
    }
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages"]
}


configurations.all {
    // Check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

def timestamp() {
    new SimpleDateFormat("dd MMMM yyyy").format(new Date())
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.txt'
        filter ReplaceTokens, tokens: [
        "pom.version": version,
        "timestamp": timestamp()
        ]
    }
}
