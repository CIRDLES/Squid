// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

// NetBeans will automatically add "run" and "debug" tasks relying on the
// "mainClass" property. You may however define the property prior executing
// tasks by passing a "-PmainClass=<QUALIFIED_CLASS_NAME>" argument.
//
// Note however, that you may define your own "run" and "debug" task if you
// prefer. In this case NetBeans will not add these tasks but you may rely on
// your own implementation.


import org.apache.tools.ant.filters.ReplaceTokens

import java.text.SimpleDateFormat

if (!hasProperty('mainClass')) {
    ext.mainClass = ''
}

apply plugin: 'antlr'

dependencies {
    compile 'com.github.cirdles:Commons:bc38781605'
    compile "org.apache.commons:commons-math3:3.6.1"
    compile group: 'com.google.guava', name: 'guava', version: '25.1-jre'
    
    implementation "com.github.cirdles:LudwigLibrary:ae99e60ae0"
    compile group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.15'

    antlr "org.antlr:antlr4:4.7.2"
    compile group: 'org.antlr', name: 'antlr4-runtime', version: '4.7.2'

    // https://mvnrepository.com/artifact/org.apache.poi/poi
    compile group: 'org.apache.poi', name: 'poi', version: '3.16'

    compile group: 'org.jdom', name: 'jdom2', version: '2.0.6'

    compile 'com.github.opencollab.jlatexmath:jlatexmath:1.0.7'
    compile group: 'com.openhtmltopdf', name: 'openhtmltopdf-pdfbox', version: '0.0.1-RC7'

    compile('javax.xml.bind:jaxb-api:2.3.0')
    compile('javax.activation:activation:1.1')
    compile('org.glassfish.jaxb:jaxb-runtime:2.3.0')

    //compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'

}

sourceSets {
    main {
        antlr {
            srcDirs = ["src/main/antlr"]
        }
    }
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages"]
}


configurations.all {
    // Check for updates every build when 0 or not when 2628000
    resolutionStrategy.cacheChangingModulesFor 2628000, 'seconds'
}

def timestamp() {
    new SimpleDateFormat("dd MMMM yyyy").format(new Date())
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.txt'
        filter ReplaceTokens, tokens: [
                "pom.version": version,
                "timestamp"  : timestamp()
        ]
    }
}