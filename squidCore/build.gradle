// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

// NetBeans will automatically add "run" and "debug" tasks relying on the
// "mainClass" property. You may however define the property prior executing
// tasks by passing a "-PmainClass=<QUALIFIED_CLASS_NAME>" argument.
//
// Note however, that you may define your own "run" and "debug" task if you
// prefer. In this case NetBeans will not add these tasks but you may rely on
// your own implementation.


import org.apache.tools.ant.filters.ReplaceTokens

import java.text.SimpleDateFormat

if (!hasProperty('mainClass')) {
    ext.mainClass = ''
}

apply plugin: 'antlr'

dependencies {
    implementation 'com.github.cirdles:commons:bc38781605'
    implementation "org.apache.commons:commons-math3:3.6.1"
    implementation group: 'com.google.guava', name: 'guava', version: '31.0.1-jre'

    implementation group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.18'

    antlr 'org.antlr:antlr4:4.9.2'
    implementation group: 'org.antlr', name: 'antlr4-runtime', version: '4.9.2'

    // https://mvnrepository.com/artifact/org.apache.poi/poi
    implementation group: 'org.apache.poi', name: 'poi', version: '5.0.0'

    implementation group: 'org.jdom', name: 'jdom2', version: '2.0.6'

    implementation 'com.github.opencollab.jlatexmath:jlatexmath:1.0.7'
    implementation group: 'com.openhtmltopdf', name: 'openhtmltopdf-pdfbox', version: '1.0.10'
}

sourceSets {
    main {
        antlr {
            srcDirs = ["src/main/antlr"]
        }
    }
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages"]
}


configurations.all {
    // Check for updates every build when 0 or not when 2628000
    resolutionStrategy.cacheChangingModulesFor 2628000, 'seconds'
}

def timestamp() {
    new SimpleDateFormat("dd MMMM yyyy").format(new Date())
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        include '**/*.txt'
        filter ReplaceTokens, tokens: [
                "pom.version": version,
                "timestamp"  : timestamp()
        ]
    }
}